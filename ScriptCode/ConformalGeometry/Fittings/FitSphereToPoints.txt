/*
 * This script fits a sphere to four non-co-planar points.
 */
do(
	/*
	 * Start with a fresh environment.
	 */
	wipe_env(),

	/*
	 * Create the points.
	 */
	bind_dual_point( p0, p1, p2, p3 ),
	
	/*
	 * Create a function that can place a conformal geometry.
	 */
	place_obj = "do(
		t = x*e0 + y*e1 + z*e2,
		T = 1 - 0.5*t*ni,
		obj = T*obj*T~
	)",
	
	/*
	 * Place the points so that they are all non-co-planar.
	 */
	p0 = do( x=-3, y=-5, z=6, obj=p0, #place_obj ),
	p1 = do( x=-5, y=3, z=8, obj=p1, #place_obj ),
	p2 = do( x=3, y=-2, z=-3, obj=p2, #place_obj ),
	p3 = do( x=0, y=1, z=-8, obj=p3, #place_obj ),
	
	/*
	 * Calculate and create the sphere that fits the points.
	 */
	bind_dual_sphere( sph ),
	formula_constraint( "sph = ( p0^p1^p2^p3 )*-I" ),
	
	/*
	 * Give all the geometries their own color.
	 */
	geo_color( p0, 1, 0, 0 ),
	geo_color( p1, 0, 1, 0 ),
	geo_color( p2, 1, 0, 1 ),
	geo_color( p3, 1, 1, 0 ),
	geo_color( sph, 0, 1, 1 ),
)